// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 21
        compileSdkVersion = 31
        targetSdkVersion = 31
    }
    repositories {
        mavenCentral()
        google()
        mavenLocal()
    }
    dependencies {
        def agpVersion = System.env.AGP_VERSION ?: "8.0.0-beta04"
        classpath "com.android.tools.build:gradle:${agpVersion}"

        if (!System.env.UPDATING_GRADLEW) {
            classpath("com.bugsnag:bugsnag-android-gradle-plugin:9000.0.0-test")
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        exclusiveContent {
            // We get React Native's Android binaries exclusively through npm,
            // from a local Maven repo inside node_modules/react-native/.
            // (The use of exclusiveContent prevents looking elsewhere like Maven Central
            // and potentially getting a wrong version.)
            filter {
                includeGroup "com.facebook.react"
            }
            forRepository {
                maven {
                    url("$rootDir/../../node_modules/react-native/android")
                }
            }
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../../node_modules/jsc-android/dist")
        }

        google()
        maven { url 'https://www.jitpack.io' }
    }
}
